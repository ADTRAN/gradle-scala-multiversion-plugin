buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.netflix.nebula:gradle-dependency-lock-plugin:4.+"
    }
}

plugins {
    id "nebula.dependency-lock" version "4.3.2"
    id "groovy"
    id "maven"
    id "java-gradle-plugin"
}

group = artifact_group
version = "${artifact_version}.${build_number}"

sourceCompatibility = 1.6
targetCompatibility = 1.6

gradlePlugin {
    plugins {
        scalaMultiVersionPlugin {
            id = "${group}.${artifact_name}"
            implementationClass = "com.adtran.ScalaMultiVersionPlugin"
        }
    }
}

dependencyLock {
    includeTransitives = true
}
task freeze(dependsOn: ['generateLock', 'saveLock'])

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}


uploadArchives {
    repositories.mavenDeployer {
        repository(url: repo_url) {
            authentication(userName: repo_username, password: repo_password)
        }

        pom.project {
            name "$artifact_group:$artifact_name"
            packaging 'jar'
            description 'Gradle plugin to build a project against multiple versions of scala'
            url 'https://github.com/ADTRAN/gradle-scala-multiversion-plugin' // TODO: check when published

            scm {
                connection 'scm:git:https://github.com/ADTRAN/gradle-scala-multiversion-plugin.git' // TODO: check when published
                developerConnection 'scm:git:ssh://git@github.com:ADTRAN/gradle-scala-multiversion-plugin.git' // TODO: check when published
                url 'https://github.com/ADTRAN/gradle-scala-multiversion-plugin' // TODO: check when published
            }

            licenses {
                license {
                    // TODO: update when decided.
                    name 'TODO, Version ???'
                    url 'http://???.org'
                    // name 'The Apache License, Version 2.0'
                    // url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    name 'Nathan Alderson'
                    email 'nathan.alderson@adtran.com'
                    organization 'ADTRAN, Inc.'
                    organizationUrl 'http://www.adtran.com/'
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.2"
}

def configureTestTask(Task task) {
    task.outputs.upToDateWhen { false }
    task.testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }
}
configureTestTask(project.tasks.test)
